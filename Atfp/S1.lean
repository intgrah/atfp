import Mathlib.Algebra.Category.MonCat.Basic
import Mathlib.Algebra.Group.Nat.Defs
import Mathlib.CategoryTheory.Category.RelCat
import Mathlib.CategoryTheory.Endofunctor.Algebra
import Mathlib.CategoryTheory.Limits.Shapes.Terminal
import Mathlib.CategoryTheory.Limits.Shapes.BinaryProducts
import Mathlib.Order.Category.PartOrd
import Mathlib.Tactic.Recall

open CategoryTheory Limits

section Exercise1

/-! Define two different monoids whose carrier is the natural numbers. -/

instance : Monoid â„• where
  one := 0
  mul := Nat.add
  mul_one := Nat.add_zero
  one_mul := Nat.zero_add
  mul_assoc := Nat.add_assoc

instance : Monoid â„• where
  one := 1
  mul := Nat.mul
  mul_one := Nat.mul_one
  one_mul := Nat.one_mul
  mul_assoc := Nat.mul_assoc

end Exercise1

section Exercise2

/-! Add a property to the definition of monoid to make it into a commutative monoid. -/

#check CommMonoid

class CommMonoid' M extends Monoid M where
  mul_comm : âˆ€ x y : M, x * y = y * x

end Exercise2

section Exercise3

/-!
What are the initial and final objects in Poset, the category of partially ordered sets and
monotone functions?
-/

/-- Partially ordered singleton set. -/
def PartOrd.terminal : PartOrd := PartOrd.of PUnit

/-- Partially ordered empty set. -/
def PartOrd.initial : PartOrd := PartOrd.of PEmpty

def PartOrd.isTerminal : IsTerminal PartOrd.terminal :=
  IsTerminal.ofUniqueHom
    (fun _ => PartOrd.ofHom âŸ¨fun _ => âŸ¨âŸ©, fun _ _ _ => le_rflâŸ©)
    (fun _ _ => PartOrd.ext fun _ => rfl)

def PartOrd.isInitial : IsInitial PartOrd.initial :=
  IsInitial.ofUniqueHom
    (fun _ => PartOrd.ofHom âŸ¨PEmpty.elim, fun x => x.elimâŸ©)
    (fun _ _ => PartOrd.ext fun x => x.elim)

instance : HasTerminal PartOrd :=
  IsTerminal.hasTerminal PartOrd.isTerminal

instance : HasInitial PartOrd :=
  IsInitial.hasInitial PartOrd.isInitial

end Exercise3

section Exercise4

/-!
What are the initial and final objects in CMon, the category of commutative monoids and
monoid homomorphisms?
-/

/-- The zero object (terminal and initial) is the commutative monoid on a singleton set. -/
def CommMonCat.zero : CommMonCat := CommMonCat.of PUnit

def CommMonCat.isTerminal : IsTerminal CommMonCat.zero :=
  IsTerminal.ofUniqueHom
    (fun _ => CommMonCat.ofHom {
      toFun _ := 1
      map_one' := rfl
      map_mul' _ _ := rfl
    })
    (fun _ _ => CommMonCat.ext fun _ => rfl)

def CommMonCat.isInitial : IsInitial CommMonCat.zero :=
  IsInitial.ofUniqueHom
    (fun _ => CommMonCat.ofHom {
      toFun _ := 1
      map_one' := rfl
      map_mul' _ _ := (one_mul 1).symm
    })
    (fun _ m => CommMonCat.ext fun âŸ¨âŸ© => m.hom.map_one)

instance : HasTerminal CommMonCat :=
  IsTerminal.hasTerminal CommMonCat.isTerminal

instance : HasInitial CommMonCat :=
  IsInitial.hasInitial CommMonCat.isInitial

end Exercise4

section Exercise5

/-!
What do products in Rel, the category of sets and relations, look like? (Hint. The product of `A`
and `B` is not the cartesian product of sets!)
-/

/-! The product is the disjoint union -/

universe u

open SetRel Function

def RelCat.prodFan (X Y : RelCat.{u}) : BinaryFan X Y :=
  BinaryFan.mk
    (.ofRel (graph Sum.inl).inv)
    (.ofRel (graph Sum.inr).inv)

private def RelCat.prodLift {W X Y : RelCat.{u}} (f : W âŸ¶ X) (g : W âŸ¶ Y) :
    W âŸ¶ (X âŠ• Y : Type u) :=
  .ofRel {p | (âˆƒ x, p.2 = .inl x âˆ§ (p.1, x) âˆˆ f.rel) âˆ¨
              (âˆƒ y, p.2 = .inr y âˆ§ (p.1, y) âˆˆ g.rel)}

private lemma RelCat.prodLift_inl {W X Y : RelCat.{u}} (f : W âŸ¶ X) (g : W âŸ¶ Y) (w x) :
    (w, Sum.inl x) âˆˆ (prodLift f g).rel â†” (w, x) âˆˆ f.rel := by
  apply Iff.intro
  Â· rintro (âŸ¨x', hx', hâŸ© | âŸ¨y', hy', _âŸ©)
    Â· exact Sum.inl.inj hx' â–¸ h
    Â· exact absurd hy' nofun
  Â· exact fun h => .inl âŸ¨x, rfl, hâŸ©

private lemma RelCat.prodLift_inr {W X Y : RelCat.{u}} (f : W âŸ¶ X) (g : W âŸ¶ Y) (w y) :
    (w, Sum.inr y) âˆˆ (prodLift f g).rel â†” (w, y) âˆˆ g.rel := by
  apply Iff.intro
  Â· rintro (âŸ¨x', hx', _âŸ© | âŸ¨y', hy', hâŸ©)
    Â· exact absurd hx' nofun
    Â· exact Sum.inr.inj hy' â–¸ h
  Â· exact fun h => .inr âŸ¨y, rfl, hâŸ©

private lemma RelCat.comp_fst_rel {W X Y : RelCat.{u}} (m : W âŸ¶ (X âŠ• Y)) w x :
    (w, x) âˆˆ (m â‰« (prodFan X Y).fst).rel â†” (w, Sum.inl x) âˆˆ m.rel :=
  âŸ¨fun âŸ¨_, hm, heqâŸ© => heq â–¸ hm, fun hm => âŸ¨_, hm, rflâŸ©âŸ©

private lemma RelCat.comp_snd_rel {W X Y : RelCat.{u}} (m : W âŸ¶ (X âŠ• Y)) w y :
    (w, y) âˆˆ (m â‰« (prodFan X Y).snd).rel â†” (w, Sum.inr y) âˆˆ m.rel :=
  âŸ¨fun âŸ¨_, hm, heqâŸ© => heq â–¸ hm, fun hm => âŸ¨_, hm, rflâŸ©âŸ©

def RelCat.prodFan_isLimit (X Y : RelCat.{u}) : IsLimit (RelCat.prodFan X Y) := by
  apply BinaryFan.isLimitMk
  case lift =>
    exact fun s => prodLift s.fst s.snd
  case fac_left =>
    intro s
    apply RelCat.Hom.ext
    ext âŸ¨w, xâŸ©
    exact (comp_fst_rel _ w x).trans (prodLift_inl _ _ w x)
  case fac_right =>
    intro s
    apply RelCat.Hom.ext
    ext âŸ¨w, yâŸ©
    exact (comp_snd_rel _ w y).trans (prodLift_inr _ _ w y)
  case uniq =>
    intro s m hfst hsnd
    ext âŸ¨w, zâŸ©
    cases z with
    | inl x =>
      rw [prodLift_inl]
      exact (comp_fst_rel m w x).symm.trans (Set.ext_iff.mp (congr_arg _ hfst) _)
    | inr y =>
      rw [prodLift_inr]
      exact (comp_snd_rel m w y).symm.trans (Set.ext_iff.mp (congr_arg _ hsnd) _)

instance (X Y : RelCat) : HasLimit (pair X Y) :=
  HasLimit.mk âŸ¨RelCat.prodFan X Y, RelCat.prodFan_isLimit X YâŸ©

instance : HasBinaryProducts RelCat :=
  hasBinaryProducts_of_hasLimit_pair RelCat

end Exercise5

section Exercise6

universe u

/-!
The signature for `Inductive` has a comment saying that out is not strictly necessary. Show that
you can implement out using `fold`, `into` and `F.map`. Why did we include it in the API
nonetheless?
-/

open Endofunctor

structure Inductive (F : Type u â¥¤ Type u) where
  /-- Carrier `alg.a` and algebra map `alg.str` -/
  alg : Algebra F
  isInitial : IsInitial alg

variable {F : Type u â¥¤ Type u} (I : Inductive F) {Î± : Type u}

def Inductive.fold (alg : F.obj Î± â†’ Î±) : I.alg.a â†’ Î± :=
  (I.isInitial.to âŸ¨Î±, algâŸ©).f

def Inductive.into : F.obj I.alg.a â†’ I.alg.a := I.alg.str

def Inductive.out : I.alg.a â†’ F.obj I.alg.a :=
  Algebra.Initial.strInv I.isInitial

def Inductive.out' : I.alg.a â†’ F.obj I.alg.a :=
  I.fold (F.map I.into)

example : I.out = I.out' := rfl

/-!
The `fold`-based implementation is O(n), which is inefficient.
However, `out` can be implemented in O(1).
-/

end Exercise6

section Exercise7

/-! Prove that `ã€šFã€›` defines a functor for all `F`. -/

universe u

/-- Grammar of polynomial functors. -/
inductive PolynomialFunctor where
  | id
  | const (A : Type u)
  | prod (F G : PolynomialFunctor)
  | coprod (F G : PolynomialFunctor)

set_option hygiene false in
/-- Turn off hygiene to allow notation to be used within its definition -/
notation "ã€š" F "ã€›" => PolynomialFunctor.denotation F

/--
Interpretation of the grammar.

We inductively show that the interpretation defines a valid functor.
-/
def PolynomialFunctor.denotation : PolynomialFunctor â†’ Type u â¥¤ Type u
  | id => ðŸ­ (Type u)
  | const A => Functor.const (Type u) |>.obj A
  | prod F G => {
      obj X := ã€šFã€›.obj X Ã— ã€šGã€›.obj X
      map f := Prod.map (ã€šFã€›.map f) (ã€šGã€›.map f)
      map_id := by
        intro
        simp only [Functor.map_id]
        rfl
      map_comp := by
        intros
        simp only [Functor.map_comp]
        rfl
    }
  | coprod F G => {
      obj X := ã€šFã€›.obj X âŠ• ã€šGã€›.obj X
      map f := Sum.map (ã€šFã€›.map f) (ã€šGã€›.map f)
      map_id := by
        intro
        simp only [Functor.map_id]
        ext a
        cases a with
        | inl => simp only [Sum.map_inl, types_id_apply]
        | inr => simp only [Sum.map_inr, types_id_apply]
      map_comp := by
        intros
        ext
        simp only [Functor.map_comp, types_comp_apply, Sum.map_map]
        rfl
    }

variable (F : PolynomialFunctor.{u})
#check ã€šFã€›

end Exercise7

section Exercise8

/-!
Recall that if an object `A` and a family of maps `f i : A â†’ X i` form a cone over a projective
diagram, the mediating map `fâ† ` can be explicitly given as `fâ† (a) = i â†¦ f i a`.
Verify that if there is any other `h : A â†’ lim X_i` such that `f_i = h â‰« Ï€_i` for every `i`,
then `h = fâ† `.
-/

universe u

variable {Î¹ : Type u} {X : Î¹ â†’ Type u} {A : Type u} (f : Î  i, A â†’ X i)

/-- Projection -/
def Ï€ (i : Î¹) : (Î  i, X i) â†’ X i := fun x => x i

/-- The mediating map -/
def mediate (a : A) : Î  i, X i := fun i => f i a

postfix:max "â† " => mediate

/-- Uniqueness -/
theorem mediate_unique (h : A â†’ Î  i, X i) (hh : âˆ€ i, Ï€ i âˆ˜ h = f i) : h = fâ†  := by
  funext (a : A) (i : Î¹)
  -- We want to show:
  show h a i = fâ†  a i
  -- We have
  have hh : Ï€ i âˆ˜ h = f i := hh i
  -- So
  calc h a i
    _ = Ï€ i (h a) := rfl -- By definition
    _ = f i a := congrFun hh a
    _ = fâ†  a i := rfl -- By definition

end Exercise8

section Exercise9

/-!
The Levenshtein distance, or edit distance, between two strings be naively computed as follows:
-/

variable {Î± : Type} [DecidableEq Î±]

/-- Levenshtein distance -/
def lev : List Î± Ã— List Î± â†’ â„•
  | (sâ‚, []) => sâ‚.length
  | ([], sâ‚‚) => sâ‚‚.length
  | (sâ‚@(câ‚ :: sâ‚'), sâ‚‚@(câ‚‚ :: sâ‚‚')) =>
    if câ‚ = câ‚‚ then
      lev (sâ‚', sâ‚‚')
    else
      min
        (min (lev (sâ‚, sâ‚‚')) (lev (sâ‚', sâ‚‚)))
        (lev (sâ‚', sâ‚‚'))
        + 1
termination_by s => s.1.length + s.2.length

#guard lev ([1, 5, 2, 3], [1, 2, 4, 3]) == 2
#guard lev ([1, 2, 3], [1, 2, 3]) == 0

/-!
Formulate this algorithm as a coalgebra-to-algebra morphism, and then solve with dynamic
programming.
-/

inductive Split (Î± R : Type)
  | inl : List Î± â†’ Split Î± R
  | inr : List Î± â†’ Split Î± R
  | cons : R â†’ Split Î± R
  | diff : R â†’ R â†’ R â†’ Split Î± R

def Split.map {R S} (f : R â†’ S) : Split Î± R â†’ Split Î± S
  | Split.inl nâ‚ => Split.inl nâ‚
  | Split.inr nâ‚‚ => Split.inr nâ‚‚
  | Split.cons x => Split.cons (f x)
  | Split.diff x y z => Split.diff (f x) (f y) (f z)

/-- Split is indeed a lawful functor -/
def Split.functor : Type â¥¤ Type where
  obj := Split Î±
  map := Split.map
  map_id := by intro; ext x; cases x <;> rfl
  map_comp := by intros; ext x; cases x <;> rfl

def Split.coalg : List Î± Ã— List Î± â†’ Split Î± (List Î± Ã— List Î±)
  | (sâ‚, []) => Split.inl sâ‚
  | ([], sâ‚‚) => Split.inr sâ‚‚
  | (sâ‚@(câ‚ :: sâ‚'), sâ‚‚@(câ‚‚ :: sâ‚‚')) =>
    if câ‚ = câ‚‚ then
      Split.cons (sâ‚', sâ‚‚')
    else
      Split.diff (sâ‚, sâ‚‚') (sâ‚', sâ‚‚) (sâ‚', sâ‚‚')

def Split.alg : Split Î± â„• â†’ â„•
  | Split.inl sâ‚ => sâ‚.length
  | Split.inr sâ‚‚ => sâ‚‚.length
  | Split.cons x => x
  | Split.diff x y z => min (min x y) z + 1

/-- Partial because well-foundedness is not checked -/
partial def Split.hylo {Î² Ïƒ} [Inhabited Î²] (coalg : Î± â†’ Split Ïƒ Î±) (alg : Split Ïƒ Î² â†’ Î²) : Î± â†’ Î² :=
  alg âˆ˜ map (hylo coalg alg) âˆ˜ coalg

def levâ‚‚ : List Î± Ã— List Î± â†’ â„• := Split.hylo Split.coalg Split.alg

#guard levâ‚‚ ([1, 5, 2, 3], [1, 2, 4, 3]) == 2
#guard levâ‚‚ ([1, 2, 3], [1, 2, 3]) == 0

/-- Version of `map` handling mutable state. -/
def Split.mapM {m F G} [Applicative m] (f : F â†’ m G) : Split Î± F â†’ m (Split Î± G)
  | .inl s => pure (.inl s)
  | .inr s => pure (.inr s)
  | .cons x => .cons <$> f x
  | .diff x y z => .diff <$> f x <*> f y <*> f z

-- We require Î± to be hashable for memoisation
variable [Hashable Î±]

/-- Memoised version for dynamic programming -/
unsafe def Split.memo {Î² Ïƒ}
    (coalg : Î± â†’ Split Ïƒ Î±)
    (alg : Split Ïƒ Î² â†’ Î²) :
    Î± â†’ Î² :=
  let rec lev (x : Î±) : StateM (Std.HashMap Î± Î²) Î² := do
    match (â† get)[x]? with
    | some v => return v
    | none => do
      let v := alg (â† Split.mapM lev (coalg x))
      modify (Â·.insert x v)
      return v
  fun x => (lev x).run' âˆ…

unsafe def levâ‚ƒ : List Î± Ã— List Î± â†’ â„• :=
  Split.memo Split.coalg Split.alg

#eval levâ‚ƒ ([1, 5, 2, 3], [1, 2, 4, 3]) == 2
#eval levâ‚ƒ ([1, 2, 3], [1, 2, 3]) == 0
#eval levâ‚ƒ (List.range 100, 1 :: List.range 100) == 1

end Exercise9
