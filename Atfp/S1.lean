import Mathlib.Algebra.Category.MonCat.Basic
import Mathlib.Algebra.Group.Nat.Defs
import Mathlib.CategoryTheory.Category.RelCat
import Mathlib.CategoryTheory.Endofunctor.Algebra
import Mathlib.CategoryTheory.Limits.Shapes.Terminal
import Mathlib.CategoryTheory.Limits.Shapes.BinaryProducts
import Mathlib.Order.Category.PartOrd
import Mathlib.Tactic.Recall

set_option linter.hashCommand false

open CategoryTheory Limits

section Exercise1

/-! Define two different monoids whose carrier is the natural numbers. -/

instance : Monoid â„• where
  one := 0
  mul := Nat.add
  mul_one := Nat.add_zero
  one_mul := Nat.zero_add
  mul_assoc := Nat.add_assoc

instance : Monoid â„• where
  one := 1
  mul := Nat.mul
  mul_one := Nat.mul_one
  one_mul := Nat.one_mul
  mul_assoc := Nat.mul_assoc

end Exercise1

section Exercise2

/-! Add a property to the definition of monoid to make it into a commutative monoid. -/

#check CommMonoid

class CommMonoid' M extends Monoid M where
  mul_comm : âˆ€ x y : M, x * y = y * x

end Exercise2

section Exercise3

/-!
What are the initial and final objects in Poset, the category of partially ordered sets and
monotone functions?
-/

/-- Partially ordered singleton set. -/
def PartOrd.terminal : PartOrd := PartOrd.of PUnit

/-- Partially ordered empty set. -/
def PartOrd.initial : PartOrd := PartOrd.of PEmpty

def PartOrd.isTerminal : IsTerminal PartOrd.terminal :=
  IsTerminal.ofUniqueHom
    (fun _ => PartOrd.ofHom âŸ¨fun _ => âŸ¨âŸ©, fun _ _ _ => le_rflâŸ©)
    (fun _ _ => PartOrd.ext fun _ => rfl)

def PartOrd.isInitial : IsInitial PartOrd.initial :=
  IsInitial.ofUniqueHom
    (fun _ => PartOrd.ofHom âŸ¨nofun, nofunâŸ©)
    (fun _ _ => PartOrd.ext nofun)

instance : HasTerminal PartOrd :=
  IsTerminal.hasTerminal PartOrd.isTerminal

instance : HasInitial PartOrd :=
  IsInitial.hasInitial PartOrd.isInitial

end Exercise3

section Exercise4

/-!
What are the initial and final objects in CMon, the category of commutative monoids and
monoid homomorphisms?
-/

/-- The zero object (terminal and initial) is the commutative monoid on a singleton set. -/
def CommMonCat.zero : CommMonCat := CommMonCat.of PUnit

def CommMonCat.isTerminal : IsTerminal CommMonCat.zero :=
  IsTerminal.ofUniqueHom
    (fun _ => ofHom âŸ¨âŸ¨fun _ => 1, rflâŸ©, fun _ _ => rflâŸ©)
    (fun _ _ => ext fun _ => rfl)

def CommMonCat.isInitial : IsInitial CommMonCat.zero :=
  IsInitial.ofUniqueHom
    (fun _ => ofHom âŸ¨âŸ¨fun âŸ¨âŸ© => 1, rflâŸ©, fun âŸ¨âŸ© âŸ¨âŸ© => (one_mul 1).symmâŸ©)
    (fun _ m => ext fun âŸ¨âŸ© => m.hom.map_one)

instance : HasTerminal CommMonCat :=
  IsTerminal.hasTerminal CommMonCat.isTerminal

instance : HasInitial CommMonCat :=
  IsInitial.hasInitial CommMonCat.isInitial

end Exercise4

section Exercise5

/-!
What do products in Rel, the category of sets and relations, look like? (Hint. The product of `A`
and `B` is not the cartesian product of sets!)
-/

/-! The product is the disjoint union (as well as the coproduct) -/

universe u

variable {W X Y : RelCat.{u}}

open SetRel Function

def RelCat.prodFan (X Y : RelCat.{u}) : BinaryFan X Y :=
  BinaryFan.mk
    (.ofRel (graph Sum.inl).inv)
    (.ofRel (graph Sum.inr).inv)

def RelCat.prodLift (f : W âŸ¶ X) (g : W âŸ¶ Y) : W âŸ¶ X âŠ• Y :=
  .ofRel {(w, xy) | match xy with | .inl x => (w, x) âˆˆ f.rel | .inr y => (w, y) âˆˆ g.rel}

lemma RelCat.comp_fst_rel (m : W âŸ¶ X âŠ• Y) w x :
    (w, x) âˆˆ (m â‰« (prodFan X Y).fst).rel â†” (w, Sum.inl x) âˆˆ m.rel :=
  âŸ¨fun âŸ¨.inl _, hm, heqâŸ© => heq â–¸ hm, fun hm => âŸ¨.inl x, hm, rflâŸ©âŸ©

lemma RelCat.comp_snd_rel (m : W âŸ¶ X âŠ• Y) w y :
    (w, y) âˆˆ (m â‰« (prodFan X Y).snd).rel â†” (w, Sum.inr y) âˆˆ m.rel :=
  âŸ¨fun âŸ¨.inr _, hm, heqâŸ© => heq â–¸ hm, fun hm => âŸ¨.inr y, hm, rflâŸ©âŸ©

def RelCat.prodFan_isLimit : IsLimit (prodFan X Y) := by
  apply BinaryFan.isLimitMk fun s => prodLift s.fst s.snd
  case fac_left => intro; ext; apply comp_fst_rel
  case fac_right => intro; ext; apply comp_snd_rel
  case uniq =>
    intro _ _ hfst hsnd
    ext âŸ¨w, x | yâŸ©
    Â· rw [â† comp_fst_rel, â† hfst]
      rfl
    Â· rw [â† comp_snd_rel, â† hsnd]
      rfl

instance (X Y : RelCat) : HasLimit (pair X Y) :=
  HasLimit.mk âŸ¨RelCat.prodFan X Y, RelCat.prodFan_isLimitâŸ©

instance : HasBinaryProducts RelCat :=
  hasBinaryProducts_of_hasLimit_pair RelCat

end Exercise5

section Exercise6

universe u

/-!
The signature for `Inductive` has a comment saying that out is not strictly necessary. Show that
you can implement out using `fold`, `into` and `F.map`. Why did we include it in the API
nonetheless?
-/

open Endofunctor

structure Inductive (F : Type u â¥¤ Type u) where
  /-- Carrier `alg.a` and algebra map `alg.str` -/
  alg : Algebra F
  isInitial : IsInitial alg

variable {F : Type u â¥¤ Type u} (I : Inductive F) {Î± : Type u}

def Inductive.fold (alg : F.obj Î± â†’ Î±) : I.alg.a â†’ Î± :=
  (I.isInitial.to âŸ¨Î±, algâŸ©).f

def Inductive.into : F.obj I.alg.a â†’ I.alg.a := I.alg.str

def Inductive.out : I.alg.a â†’ F.obj I.alg.a :=
  Algebra.Initial.strInv I.isInitial

def Inductive.out' : I.alg.a â†’ F.obj I.alg.a :=
  I.fold (F.map I.into)

example : I.out = I.out' := rfl

/-!
The `fold`-based implementation is O(n), which is inefficient.
However, `out` can be implemented in O(1).
-/

end Exercise6

section Exercise7

/-! Prove that `ã€šFã€›` defines a functor for all `F`. -/

universe u

/-- Grammar of polynomial functors. -/
inductive PolynomialFunctor where
  | id
  | const (A : Type u)
  | prod (F G : PolynomialFunctor)
  | coprod (F G : PolynomialFunctor)

set_option hygiene false in
/-- Turn off hygiene to allow notation to be used within its definition -/
notation "ã€š" F "ã€›" => PolynomialFunctor.denotation F

/--
Interpretation of the grammar.

We inductively show that the interpretation defines a valid functor.
-/
def PolynomialFunctor.denotation : PolynomialFunctor â†’ Type u â¥¤ Type u
  | id => ðŸ­ (Type u)
  | const A => Functor.const (Type u) |>.obj A
  | prod F G => {
      obj X := ã€šFã€›.obj X Ã— ã€šGã€›.obj X
      map f := Prod.map (ã€šFã€›.map f) (ã€šGã€›.map f)
      map_id := by
        intro
        simp only [Functor.map_id]
        rfl
      map_comp := by
        intros
        simp only [Functor.map_comp]
        rfl
    }
  | coprod F G => {
      obj X := ã€šFã€›.obj X âŠ• ã€šGã€›.obj X
      map f := Sum.map (ã€šFã€›.map f) (ã€šGã€›.map f)
      map_id := by
        intro
        simp only [Functor.map_id]
        ext a
        cases a with
        | inl => simp only [Sum.map_inl, types_id_apply]
        | inr => simp only [Sum.map_inr, types_id_apply]
      map_comp := by
        intros
        ext
        simp only [Functor.map_comp, types_comp_apply, Sum.map_map]
        rfl
    }

variable (F : PolynomialFunctor.{u})
#check ã€šFã€›

end Exercise7

section Exercise8

/-!
Recall that if an object `A` and a family of maps `f i : A â†’ X i` form a cone over a projective
diagram, the mediating map `fâ† ` can be explicitly given as `fâ† (a) = i â†¦ f i a`.
Verify that if there is any other `h : A â†’ lim X_i` such that `f_i = h â‰« Ï€_i` for every `i`,
then `h = fâ† `.
-/

universe u

variable {Î¹ : Type u} {X : Î¹ â†’ Type u} {A : Type u} (f : Î  i, A â†’ X i)

/-- Projection -/
def Ï€ (i : Î¹) : (Î  i, X i) â†’ X i := fun x => x i

/-- The mediating map -/
def mediate (a : A) : Î  i, X i := fun i => f i a

postfix:max "â† " => mediate

/-- Uniqueness -/
theorem mediate_unique (h : A â†’ Î  i, X i) (hh : âˆ€ i, Ï€ i âˆ˜ h = f i) : h = fâ†  := by
  show h = fâ† 
  -- Let a : A and i : Î¹
  funext (a : A) (i : Î¹)
  -- We want to show
  show h a i = fâ†  a i
  -- We have
  have hh : Ï€ i âˆ˜ h = f i := hh i
  -- So
  calc h a i
    _ = Ï€ i (h a) := rfl -- By definition
    _ = f i a := congrFun hh a
    _ = fâ†  a i := rfl -- By definition

end Exercise8

section Exercise9

/-!
The Levenshtein distance, or edit distance, between two strings be naively computed as follows:
-/

variable {Î± : Type} [DecidableEq Î±] [Hashable Î±]

/-- Levenshtein distance -/
def lev : List Î± Ã— List Î± â†’ â„•
  | (sâ‚, []) => sâ‚.length
  | ([], sâ‚‚) => sâ‚‚.length
  | (sâ‚@(câ‚ :: sâ‚'), sâ‚‚@(câ‚‚ :: sâ‚‚')) =>
    if câ‚ = câ‚‚ then
      lev (sâ‚', sâ‚‚')
    else
      min
        (min (lev (sâ‚, sâ‚‚')) (lev (sâ‚', sâ‚‚)))
        (lev (sâ‚', sâ‚‚'))
        + 1
termination_by s => s.1.length + s.2.length

/- It works -/
#guard lev ([1, 5, 2, 3], [1, 2, 4, 3]) == 2
#guard lev ([1, 2, 3], [1, 2, 3]) == 0

/-!
Formulate this algorithm as a coalgebra-to-algebra morphism, and then solve with dynamic
programming.
-/

inductive Split.obj (Î± R : Type)
  | inl (sâ‚ : List Î±)
  | inr (sâ‚‚ : List Î±)
  | cons (x : R)
  | diff (x y z : R)

def Split.map {R S} (f : R â†’ S) : obj Î± R â†’ obj Î± S
  | .inl sâ‚ => .inl sâ‚
  | .inr sâ‚‚ => .inr sâ‚‚
  | .cons x => .cons (f x)
  | .diff x y z => .diff (f x) (f y) (f z)

/-- Split is indeed a lawful functor -/
def Split (Î± : Type) : Type â¥¤ Type where
  obj := Split.obj Î±
  map := Split.map
  map_id _ := by ext (_ | _ | _ | _) <;> rfl
  map_comp _ _ := by ext (_ | _ | _ | _) <;> rfl

open Endofunctor (Algebra Coalgebra)

abbrev Split.coalg : Coalgebra (Split Î±) where
  V := List Î± Ã— List Î± -- carrier
  str -- structure morphism from V âŸ¶ Split.obj Î± V
    | (sâ‚, []) => .inl sâ‚
    | ([], sâ‚‚) => .inr sâ‚‚
    | (sâ‚@(câ‚ :: sâ‚'), sâ‚‚@(câ‚‚ :: sâ‚‚')) =>
      if câ‚ = câ‚‚ then
        .cons (sâ‚', sâ‚‚')
      else
        .diff (sâ‚, sâ‚‚') (sâ‚', sâ‚‚) (sâ‚', sâ‚‚')

abbrev Split.alg : Algebra (Split Î±) where
  a := â„• -- carrier
  str -- structure morphism from Split.obj Î± a âŸ¶ a
    | .inl sâ‚ => sâ‚.length
    | .inr sâ‚‚ => sâ‚‚.length
    | .cons x => x
    | .diff x y z => min (min x y) z + 1

/-- Partial because well-foundedness is not proven -/
partial def Split.hylo {Î±}
    (coalg : Coalgebra (Split Î±))
    (alg : Algebra (Split Î±)) [Inhabited alg.a] :
    coalg.V â†’ alg.a :=
  alg.str âˆ˜ map (hylo coalg alg) âˆ˜ coalg.str

def levâ‚‚ : List Î± Ã— List Î± â†’ â„• := Split.hylo Split.coalg Split.alg

/- It works -/
#guard levâ‚‚ ([1, 5, 2, 3], [1, 2, 4, 3]) == 2
#guard levâ‚‚ ([1, 2, 3], [1, 2, 3]) == 0

/-- Version of `map` handling mutable state. -/
def Split.mapM {m F G} [Applicative m] (f : F â†’ m G) : obj Î± F â†’ m (obj Î± G)
  | .inl s => pure (.inl s)
  | .inr s => pure (.inr s)
  | .cons x => .cons <$> f x
  | .diff x y z => .diff <$> f x <*> f y <*> f z

/-- Memoised version for dynamic programming -/
unsafe def Split.memo
    (coalg : Coalgebra (Split Î±)) [BEq coalg.V] [Hashable coalg.V]
    (alg : Algebra (Split Î±)) :
    coalg.V â†’ alg.a :=
  let rec lev (x : coalg.V) : StateM (Std.HashMap coalg.V alg.a) alg.a := do
    match (â† get)[x]? with
    | some v => return v
    | none => do
      let v := alg.str (â† Split.mapM lev (coalg.str x))
      modify (Â·.insert x v)
      return v
  fun x => (lev x).run' âˆ…

unsafe def levâ‚ƒ : List Î± Ã— List Î± â†’ â„• :=
  Split.memo Split.coalg Split.alg

/-- info: 2 -/
#guard_msgs in
#eval levâ‚ƒ ([1, 5, 2, 3], [1, 2, 4, 3])
/-- info: 0 -/
#guard_msgs in
#eval levâ‚ƒ ([1, 2, 3], [1, 2, 3])

/- [0, ... 99] and [1, 0, ... 99] would be intractable if not for memoisation -/
/-- info: 1 -/
#guard_msgs in
#eval levâ‚ƒ (List.range 100, 1 :: List.range 100)

end Exercise9
